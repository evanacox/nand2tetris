/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {
    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or to continue 
                         // executing the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
        ARegister(in=inAReg, load=loadA, out=outAreg, out[0..14]=addressM); // outAreg == addressM
        DRegister(in=outALU, load=loadD, out=outDReg);
        PC(in=outAreg, inc=PCinc, load=loadPC, reset=reset, out[0..14]=pc);

        Not(in=instruction[15], out=isAInstruction); 
        Not(in=isAInstruction, out=isCInstruction);
        
        // if it's a C instruction and A is dest
        And(a=isCInstruction, b=instruction[5], out=isCWriteA);

        // write A if it's an A instruction or the destination is A
        Or(a=isAInstruction, b=isCWriteA, out=loadA);

        // if it's a C instruction and dest is D, D needs to be loaded
        And(a=isCInstruction, b=instruction[4], out=loadD);                  
        
        // A or M, sel=a (a bit selects A register)
        Mux16(a=outAreg, b=inM, sel=instruction[12], out=outAorM);            
        
        ALU(x=outDReg,
            y=outAorM,
            zx=instruction[11],   // c1
            nx=instruction[10],   // c2
            zy=instruction[9],    // c3
            ny=instruction[8],    // c4
            f =instruction[7],    // c5
            no=instruction[6],    // c6
            out=outALU,
            out=outM,
            zr=isZeroOut,
            ng=isNegOut);
            
        Mux16(a=instruction, 
            b=outALU, 
            sel=isCWriteA, 
            out=inAReg);
        
        Not(in=isNegOut, out=isNonNeg);
        Not(in=isZeroOut, out=isNonZero);
        And(a=isNonNeg, b=isNonZero, out=isPositive);
        
        And(a=isCInstruction, b=instruction[3], out=writeM);
        
        And(a=isPositive, b=instruction[0], out=JGT);
        And(a=isZeroOut, b=instruction[1], out=JEQ);
        And(a=isNegOut, b=instruction[2], out=JLT);
            
        Or(a=JEQ, b=JLT, out=JLE);
        Or(a=JLE, b=JGT, out=jumpToA);
        And(a=isCInstruction, b=jumpToA, out=loadPC);
        Not(in=loadPC, out=PCinc);
}